import java.text.SimpleDateFormat

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

apply from: "../ext.gradle"

def rootConfig = rootProject.ext

group rootConfig.group
version rootConfig.pigeon.version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    api 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    api 'com.google.code.gson:gson:2.8.9'
    api 'com.google.guava:guava:31.0.1-jre'
    api 'org.jetbrains:annotations:22.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
    compileOnly 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.0-alpha1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.warnings = false
    options.deprecation = true
    options.compilerArgs += ["-parameters"]
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

compileJava.dependsOn(clean)

// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
    enabled true
    classifier ''
    afterEvaluate {
        manifest {
            attributes 'Implementation-Version': archiveVersion
            attributes 'Built-Gradle': gradle.gradleVersion
            attributes 'Bundle-DocURL': 'https://github.com/yhyzgn/'
            attributes 'Build-OS': System.getProperty("os.name")
            attributes 'Built-By': System.getProperty("user.name")
            attributes 'Build-Jdk': System.getProperty("java.version")
            attributes 'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date())
        }
    }
}

javadoc {
    description = "生成jar格式的javadoc。"
    // 只显示 protected 和 public 的类和成员
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    // 静默javadoc检查（比如不支持@date会报错等），jdk 8+
    options.addStringOption('Xdoclint:none', '-quiet')
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    // suppress "## warnings" message
    logging.captureStandardOutput LogLevel.INFO
    // 编码一定要配置否则直接出错
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    // java9
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task replaceVersion() {
    ant.replaceregexp(
            file: "./src/main/java/com/yhy/http/pigeon/utils/Utils.java",
            match: "public final static String VERSION = \"(.+)\"",
            replace: "public final static String VERSION = \"${rootConfig.pigeon.version}\"",
            flags: 'g',
            byline: "true"
    )
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

javadoc {
    afterEvaluate {
        configure(options) {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            failOnError false
            links "http://docs.oracle.com/javase/8/docs/api"
        }
    }
}

compileJava.dependsOn(clean, replaceVersion)
publish.dependsOn(publishToMavenLocal)

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = rootConfig.group
            artifactId = rootConfig.pigeon.artifact
            version = rootConfig.pigeon.version
            from components.java

            pom {
                name = rootConfig.pigeon.artifact
                description = rootConfig.pigeon.description
                url = "https://${rootConfig.url}"
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        name = 'yhyzgn'
                        email = 'yhyzgn@gmail.com'
                        url = "https://github.com/yhyzgn/"
                    }
                }
                scm {
                    url = "https://${rootConfig.url}.git"
                    connection = "scm:git:https://${rootConfig.url}.git"
                    developerConnection = "scm:git:https://${rootConfig.url}.git"
                }
            }
        }
    }

    repositories {
        maven {
            name "oss"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}